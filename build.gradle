/*
 * # Write the following settings to ~/.gradle/gradle.properties
 *
 * REPO_DEPLOYMENT_USERNAME=deployment
 * REPO_DEPLOYMENT_PASSWORD=deployment123
 * REPO_DEPLOYMENT_URL=http://dns-of-server:8081/nexus/content/repositories
 *
 */

/*
 * The gradle wrapper generation.
 */
task wrapper(type:Wrapper) {
  gradleVersion = '1.10'
}

apply from: 'package.gradle'
apply plugin: 'maven-publish'

publishing {
  publications {
    packages.each { pkg ->
      "${pkg.target}"(MavenPublication) {
        artifactId  pkg.target
        groupId     pkg.group ?: project.groupId
        version     pkg.version ?: project.version
        artifact source: file(relativePath(pkg.source)), classifier: pkg.classifier, extension: pkg.extension
      }
    }
    repositories.maven {
      credentials {
        username REPO_DEPLOYMENT_USERNAME
        password REPO_DEPLOYMENT_PASSWORD
      }
      if ( repository.equals('thirdparty') ) {
        url REPO_DEPLOYMENT_URL + "/thirdparty"
      } else {
        if(version.endsWith('-SNAPSHOT')){
          url REPO_DEPLOYMENT_URL + "/snapshots"
        } else {
          url REPO_DEPLOYMENT_URL + "/releases"
        }
      }
    }
  }
}

